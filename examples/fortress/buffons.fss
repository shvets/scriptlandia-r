(*******************************************************************************
    Copyright 2007 Sun Microsystems, Inc.,
    4150 Network Circle, Santa Clara, California 95054, U.S.A.
    All rights reserved.

    U.S. Government Rights - Commercial software.
    Government users are subject to the Sun Microsystems, Inc. standard
    license agreement and applicable provisions of the FAR and its supplements.

    Use is subject to license terms.

    This distribution may include materials developed by third parties.

    Sun, Sun Microsystems, the Sun logo and Java are trademarks or registered
    trademarks of Sun Microsystems, Inc. in the U.S. and other countries.
 ******************************************************************************)

component fortress.executable

export Executable

run(args:String...):()=do
   needleLength = 20
   numRows = 10
   tableHeight = needleLength numRows
   var hits : RR64 := 0.0
   var n : RR64 := 0.0

   println("Starting parallel Buffons")
   recordTime(6.0)
   for i <- 1#3000 do
      delta_X = random(2.0) - 1
      delta_Y = random(2.0) - 1
      rsq = delta_X^2 + delta_Y^2
      if 0 < rsq < 1 then
         y1 = tableHeight random(1.0)
         y2 = y1 + needleLength (delta_Y / SQRT rsq)
         (y_L, y_H) = (y1 MIN y2, y1 MAX y2)
         if ceiling(y_L/needleLength) = floor(y_H/needleLength) then
                    atomic do hits += 1.0 end
         end
         atomic do n += 1.0 end
      end
   end
   probability = hits/n
   pi_est = 2.0/probability
   printTime(6.0)
   println("")
   print("estimated Pi = ")
   println(pi_est)
   end
end
